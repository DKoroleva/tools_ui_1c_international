
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КопироватьДанныеФормы(Параметры.Объект, Объект);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	
	Заголовок = Параметры.Заголовок + " (таблица значений)";
	
	Таблица = Обработка.StringToValue(Параметры.Значение.Значение);
	
	Обработка.CreateTableAttributesByColumns(ЭтаФорма, "ТаблицаЗначений", "ТаблицаЗначенийСоответствиеКолонок", "ТаблицаЗначенийКолонкиКонтейнера", Таблица.Колонки, Истина);

	Обработка.TableToFormAttribute(Таблица, ТаблицаЗначений, ТаблицаЗначенийКолонкиКонтейнера);
	
	фТолькоПросмотр = Ложь;
	Если Параметры.Свойство("ТолькоПросмотр", фТолькоПросмотр) И фТолькоПросмотр = Истина Тогда
		Элементы.ТаблицаЗначений.ТолькоПросмотр = Истина;
		Элементы.КнопкаОК.Видимость = Ложь;
	КонецЕсли;

	ContainerAttributeSuffix=Обработка.ContainerAttributeSuffix();
КонецПроцедуры

&НаСервере
Функция ПолучитьВозвращаемуюТаблицу()
	Обработка = РеквизитФормыВЗначение("Объект");
	тзВозвращаемаяТаблица = Обработка.TableFromFormAttributes(ТаблицаЗначений, ТаблицаЗначенийКолонкиКонтейнера);
	Возврат Обработка.Container_SaveValue(тзВозвращаемаяТаблица);
КонецФункции

&НаСервере
Процедура InitializeRowContainersByTypes(чСтрока, ТаблицаЗначенийКолонкиКонтейнера)
	РеквизитФормыВЗначение("Объект").InitializeRowContainersByTypes(ТаблицаЗначений.НайтиПоИдентификатору(чСтрока), ТаблицаЗначенийКолонкиКонтейнера);
КонецПроцедуры

&НаКлиенте
Функция ПолноеИмяФормы(ИмяФормы)
	Возврат СтрШаблон("%1.Форма.%2", Объект.MetadataPath, ИмяФормы);
КонецФункции

//@skip-warning
&НаКлиенте
Процедура ПолеТаблицыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Перем Контейнер;
	
	ИмяКолонки = ТаблицаЗначенийСоответствиеКолонок[Элемент.Имя];
	ИмяКолонкиКонтейнера = ИмяКолонки + ContainerAttributeSuffix;
	
	Если ТаблицаЗначенийКолонкиКонтейнера.Свойство(ИмяКолонки) Тогда
		
		СтрокаТаблицы = ТаблицаЗначений.НайтиПоИдентификатору(Элементы.ТаблицаЗначений.ТекущаяСтрока);
		Контейнер = СтрокаТаблицы[ИмяКолонкиКонтейнера];
		
		Если НЕ ЗначениеЗаполнено(Контейнер) Тогда
			InitializeRowContainersByTypes(Элементы.ТаблицаЗначений.ТекущаяСтрока, ТаблицаЗначенийКолонкиКонтейнера);
			Контейнер = СтрокаТаблицы[ИмяКолонкиКонтейнера];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Контейнер.Тип) Тогда
			
			Если Контейнер.Тип = "Тип" Тогда
				СтандартнаяОбработка = Ложь;
				ПараметрыОповещения = Новый Структура("Таблица, Строка, Поле", "ТаблицаЗначений", Элементы.ТаблицаЗначений.ТекущаяСтрока, ИмяКолонки);
				ОписаниеОповещенияОЗакрытииОткрываемойФормы = Новый ОписаниеОповещения("ОкончаниеРедактированияСтроки", ЭтаФорма, ПараметрыОповещения);
				ПараметрыОткрытия = Новый Структура("Объект, ТипЗначения", Объект, Контейнер);
				ОткрытьФорму(ПолноеИмяФормы("РедактированиеТипа"), ПараметрыОткрытия, ЭтаФорма, Истина, , , ОписаниеОповещенияОЗакрытииОткрываемойФормы, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			ИначеЕсли Контейнер.Тип = "МоментВремени" Тогда
				СтандартнаяОбработка = Ложь;
				ПараметрыОповещения = Новый Структура("Таблица, Строка, Поле", "ТаблицаЗначений", Элементы.ТаблицаЗначений.ТекущаяСтрока, ИмяКолонки);
				ОписаниеОповещенияОЗакрытииОткрываемойФормы = Новый ОписаниеОповещения("ОкончаниеРедактированияСтроки", ЭтаФорма, ПараметрыОповещения);
				ПараметрыОткрытия = Новый Структура("Объект, Значение", Объект, Контейнер);
				ОткрытьФорму(ПолноеИмяФормы("РедактированиеГраницыМомента"), ПараметрыОткрытия, ЭтаФорма, Ложь, , , ОписаниеОповещенияОЗакрытииОткрываемойФормы, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеРедактированияСтроки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		чИдентификаторСтроки = ДополнительныеПараметры.Строка;
		Значение = Неопределено;
		Если РезультатЗакрытия.Свойство("Значение", Значение) Тогда
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле + ContainerAttributeSuffix] = Значение;
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле] = Значение.Представление;
		Иначе
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле + ContainerAttributeSuffix] = РезультатЗакрытия.ОписаниеКонтейнера;
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле] = РезультатЗакрытия.ОписаниеКонтейнера.Представление;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОчистить(Команда)
	ТаблицаЗначений.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	ВозвращаемоеЗначение = Новый Структура("Значение", ПолучитьВозвращаемуюТаблицу());
	Закрыть(ВозвращаемоеЗначение);
КонецПроцедуры



