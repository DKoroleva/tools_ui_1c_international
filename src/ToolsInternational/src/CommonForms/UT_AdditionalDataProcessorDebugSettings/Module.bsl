&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МассивНовыхРеквизитов=Новый Массив;
	МассивНовыхРеквизитов.Добавить(Новый РеквизитФормы("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"), "", "Пользователь", Истина));
	МассивНовыхРеквизитов.Добавить(Новый РеквизитФормы("ДополнительнаяОбработка", Новый ОписаниеТипов("СправочникСсылка.AdditionalReportsAndDataProcessors"), "", "Дополнительная обработка", Истина));
	
	ИзменитьРеквизиты(МассивНовыхРеквизитов,);

	ДополнительнаяОбработка=Параметры.ДополнительнаяОбработка;
	
	ЭтотОбъект.ДополнительнаяОбработка = ДополнительнаяОбработка;

	Вид = UT_Common.ObjectAttributeValue(ДополнительнаяОбработка, "Вид");
	Если Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.Отчет Или Вид
		= Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительныйОтчет Тогда
		ЭтоОтчет=Истина;
	КонецЕсли;
	
	
	ОписаниеЭлемента=UT_Forms.ItemAttributeNewDescription();
	ОписаниеЭлемента.Имя="Пользователь";
	ОписаниеЭлемента.ПутьКДанным="Пользователь";
	UT_Forms.CreateItemByDescription(ЭтотОбъект, ОписаниеЭлемента);
	
	
	СохраненныеНастройки=UT_Common.AdditionalDataProcessorDebugSettings(ДополнительнаяОбработка);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СохраненныеНастройки);
КонецПроцедуры

&НаСервере
Процедура ПрименитьНаСервере()
	СтруктураНастройки=UT_Common.NewStructureOfAdditionalDataProcessorDebugSettings();
	ЗаполнитьЗначенияСвойств(СтруктураНастройки, ЭтотОбъект);
	
	UT_Common.SaveAdditionalDataProcessorDebugSettings(ЭтотОбъект.ДополнительнаяОбработка, СтруктураНастройки);	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	ПрименитьНаСервере();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНаСервереНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтруктураОписанияВыбираемогоФайла=UT_CommonClient.EmptyDescriptionStructureOfSelectedFile();
	СтруктураОписанияВыбираемогоФайла.ИмяФайла=ИмяФайлаНаСервере;

	Если ЭтоОтчет Тогда
		UT_CommonClient.AddFormatToSavingFileDescription(СтруктураОписанияВыбираемогоФайла,
			"Отчет (*.erf)", "erf");
	Иначе
		UT_CommonClient.AddFormatToSavingFileDescription(СтруктураОписанияВыбираемогоФайла,
			"Обработка (*.epf)", "epf");
	КонецЕсли;

	UT_CommonClient.FormFieldFileNameStartChoice(СтруктураОписанияВыбираемогоФайла, Элемент, ДанныеВыбора,
		СтандартнаяОбработка, РежимДиалогаВыбораФайла.Открытие,
		Новый ОписаниеОповещения("ИмяФайлаНаСервереНачалоВыбораЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНаСервереНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Количество()=0 Тогда
		Возврат;
	КонецЕсли;

	ИмяФайлаНаСервере=Результат[0];
КонецПроцедуры